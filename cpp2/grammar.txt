start: program

program: decl+

decl: variable_decl
    | function_decl
    | class_decl
    | interface_decl

variable_decl: variable ";"

variable: type IDENT

type: "int" -> int
    | "double" -> double
    | "bool" -> bool
    | "string" -> str
    | IDENT
    | type /\[[ \n]*\]/

function_decl: type IDENT "(" formals ")" stmt_block
             | "void" IDENT "(" formals ")" stmt_block

formals: variables?

variables: variable ("," variable)*

class_decl: "class" IDENT ("extends" IDENT)? ("implements" IDENT ("," IDENT)*)? "{" field* "}"

field: variable_decl
     | function_decl

interface_decl: "interface" IDENT "{" prototype* "}"

prototype: type IDENT "(" formals ")" ";"
         | "void" IDENT "(" formals ")" ";"

stmt_block: push_scope "{" variable_decl* stmt* "}" pop_scope
push_scope: -> push_scope
pop_scope: -> pop_scope

stmt: if_stmt

non_if_stmt:  while_stmt
    | for_stmt
    | BREAK_STMT -> break_stmt
    | return_stmt
    | print_stmt
    | stmt_block
    | expr? ";"

if_stmt: ms
       | us

ms: "if" be if_cond ms "else" if_out_label ms
  | non_if_stmt

us: "if" be if_cond stmt if_out_label
  | "if" be if_cond ms "else" if_out_label us
if_cond:
if_out_label:

be: "(" expr ")"

while_stmt: make_start_label "while" "(" expr ")" make_condition_jump stmt make_loop_jump
make_start_label:
make_condition_jump:
make_loop_jump:

for_stmt: "for" "(" expr? ";" make_start_label expr for_jump ";" expr? step_jump ")" stmt return_jump
step_jump:
for_jump:
return_jump:

return_stmt: "return" expr? ";"



print_stmt: "Print" "(" exprs ")" ";"

expr: e_layer1

e_layer1: e_layer9 "=" e_layer1
        | e_layer2

e_layer2: e_layer3 "||" e_layer2
        | e_layer3

e_layer3: e_layer4 "&&" e_layer3
        | e_layer4

e_layer4: e_layer5 "==" e_layer4
        | e_layer5 "!=" e_layer4
        | e_layer5

e_layer5: e_layer6 "<" e_layer5
        | e_layer6 "<=" e_layer5
        | e_layer6 ">" e_layer5
        | e_layer6 ">=" e_layer5
        | e_layer6

e_layer6: e_layer7 "+" e_layer6
        | e_layer7 "-" e_layer6
        | e_layer7

e_layer7: e_layer8 "*" e_layer7
        | e_layer8 "/" e_layer7
        | e_layer8 "%" e_layer7
        | e_layer8

e_layer8: "-"e_layer8
        | "!"e_layer8
        | e_layer9

e_layer9:  e_layer10 "." e_layer9
        | e_layer10

e_layer10:  e_layer10 "[" e_layer1 "]"
        | end_layer

end_layer: "ReadLine" "(" ")"
         | "NewArray" "(" e_layer1 "," type ")"
         | "ReadInteger" "(" ")"
         | call
         | CONSTANT
         | "this"
         | "(" e_layer1 ")"
         | "new" IDENT
         | IDENT

BREAK_STMT: "break" ";"

call: IDENT "(" actuals ")"

actuals: exprs?

exprs: expr ("," expr)*

CONSTANT: DOUBLE_INT
        | INT_CONSTANT
        | BOOL_CONSTANT
        | STRING_CONSTANT
        | "null"

DOUBLE_INT: DECIMAL "." /[0-9]*([eE][-+]?[0-9]+)?/

BOOL_CONSTANT: "true"
             | "false"

IDENT: /(?!(break|return|true|false|this|new)\b)[a-zA-Z]\w{0,30}/

INT_CONSTANT: HEX
            | DECIMAL

HEX: /0[xX][0-9a-fA-F]+/

DECIMAL: /[0-9]+/

STRING_CONSTANT: /\"((\\\")|[^"])*\"/


COMMENT: /\/\*([^*]|[\r\n]|(\*+([^*\/]|[\r\n])))*\*+\//
       | "//"/.*/


%import common.WS

%ignore WS
%ignore COMMENT
